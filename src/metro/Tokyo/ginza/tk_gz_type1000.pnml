//purchase
spriteset(tk_gz_type1000_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_purchase_1c(0, 0) }
spriteset(tk_gz_type1000_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_purchase_2c(0, 25) }
GRAPHIC_STACK_4(tk_gz_type1000_purchase, logo_tokyo_metro, logo_line_tk_ginza, tk_gz_type1000_purchase_c1, tk_gz_type1000_purchase_c2)

spriteset(tk_gz_type1000_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_std(0, 0) }
spriteset(tk_gz_type1000_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_std(0, 25) }
spriteset(tk_gz_type1000_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_std_rev(0, 50) }
spriteset(tk_gz_type1000_1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_std(0, 75) }
spriteset(tk_gz_type1000_1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_std(0, 100) }
spriteset(tk_gz_type1000_1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/ginza/type1000/type1000.png"){ tmpl_std_rev(0, 125) }
//setup
//basic gfx
// livery 1
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1000_end_gfx,position_in_consist_from_end){
    0: tk_gz_type1000_end;
    tk_gz_type1000_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1000_livery_1,position_in_consist){
    0: tk_gz_type1000_front;
    sw_tk_gz_t1000_end_gfx;
}
// livery 2
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1000_end_1_gfx,position_in_consist_from_end){
    0: tk_gz_type1000_1_end;
    tk_gz_type1000_1_wag;
}
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1000_livery_2,position_in_consist){
    0: tk_gz_type1000_1_front;
    sw_tk_gz_t1000_end_1_gfx;
}
//livery-randomizer
random_switch(FEAT_TRAINS, PARENT, sw_tk_gz_t1000_livery_0){
    3: sw_tk_gz_t1000_livery_1;
    1: sw_tk_gz_t1000_livery_2;
}
//livery selector
switch(FEAT_TRAINS, PARENT, sw_tk_gz_t1000_lv, cargo_subtype){
    0: sw_tk_gz_t1000_livery_0;
    1: sw_tk_gz_t1000_livery_1;
    2: sw_tk_gz_t1000_livery_2;
}
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1000_lv_desc, cargo_subtype){
    0: string(LV_RANDOM);
    1: string(LV_tk_gz_type1000_1);
    2: string(LV_tk_gz_type1000_2);
}
//attachable vehicles
switch(FEAT_TRAINS, SELF, sw_tk_gz_t1000_attach_vehid, vehicle_type_id) {
    mu_car: return string(STR_ATTACH_1435_M_ERROR);
    mu_car_narrow: return string(STR_ATTACH_1435_M_ERROR);
    return CB_RESULT_ATTACH_ALLOW;
  }

item(FEAT_TRAINS, tk_gz_t1000, 1000) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_EIDAN), "1000", string(STR_NAME_TYPE));
        introduction_date: date(1927, 1, 1);
        model_life: 11;
        vehicle_life: 48;

        cargo_capacity: 90;
        loading_speed: param_loading_3D;
        cost_factor: 15;
        running_cost_factor: 45;

        speed: 55 km/h;
        track_type: TR_1435_M;
        power: 180 kW;
        weight: 35 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);

        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        default: sw_tk_gz_t1000_lv;
        purchase: tk_gz_type1000_purchase;
        can_attach_wagon: sw_tk_gz_t1000_attach_vehid;
        additional_text:    string(STR_CONCAT_6,
                            string(STR_STANDARDGAUGE),
                            string(STR_FORMATION),
                            string(STR_NUM_CARS_RANGE, 1, 4),
                            string(STR_OPERATOR,
                            string(STR_TOKYOMETRO),
                            ),
                            string(STR_LINES_USED,
                            string(STR_TOKYO_GINZA)
                            ),
                        );
        cargo_subtype_text: sw_tk_gz_t1000_lv_desc;
        length: 8;
        cargo_capacity: 90*param_capacity_mod/3;
    }
    livery_override(mu_car_metro){
        cargo_subtype_text: sw_tk_gz_t1000_lv_desc;
        default:                      sw_tk_gz_t1000_lv;
        cargo_capacity:               80*param_capacity_mod/3;
        loading_speed:                param_loading_3D;
        power: 241;
        weight: 30;
        length: 8;
      }
}
