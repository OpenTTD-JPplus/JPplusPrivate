//purchase
spriteset(tk_hb_13000series_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_purchase_1c(0, 0) }
spriteset(tk_hb_13000series_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_purchase_2c(0, 75) }
GRAPHIC_STACK_4(tk_hb_13000series_purchase, logo_tokyo_metro, logo_line_tk_hibiya, tk_hb_13000series_purchase_c1, tk_hb_13000series_purchase_c2)

spriteset(tk_hb_13000series_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_high_skirt(0, 0) }
spriteset(tk_hb_13000series_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_high_skirt_rev(0, 25) }
spriteset(tk_hb_13000series_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_symm(0, 50) }
spriteset(tk_hb_13000series_mot_2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_symm(0, 75) }
spriteset(tk_hb_13000series_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_std(0, 100) }
spriteset(tk_hb_13000series_mot_rev, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/metro/Tokyo/hibiya/13000series/13000series.png"){ tmpl_std_rev(0, 100) }

//*livery 1
  //panto-placers
    //var 1
    switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_panto_placer_1,position_in_vehid_chain %4){
      0: tk_hb_13000series_mot;
      2: tk_hb_13000series_mot_2;
      tk_hb_13000series_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_panto_placer_1_rev,position_in_vehid_chain_from_end %4 -1){
      0: tk_hb_13000series_mot_rev;
      2: tk_hb_13000series_mot_2;
      tk_hb_13000series_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_tk_hb_13000series_panto_placer_main_1,vehicle_is_reversed){
      1: sw_tk_hb_13000series_panto_placer_1_rev;
      sw_tk_hb_13000series_panto_placer_1;
    }
    //var 2
    switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_panto_placer_2,position_in_vehid_chain %3){
      0: tk_hb_13000series_mot_2;
      tk_hb_13000series_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_panto_placer_2_rev,position_in_vehid_chain_from_end %3 -1){
      0: tk_hb_13000series_mot_2;
      tk_hb_13000series_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_tk_hb_13000series_panto_placer_main_2,vehicle_is_reversed){
      1: sw_tk_hb_13000series_panto_placer_2_rev;
      sw_tk_hb_13000series_panto_placer_2;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_panto_placer_main,num_vehs_in_vehid_chain+1){
      3..4: sw_tk_hb_13000series_panto_placer_main_2;
      sw_tk_hb_13000series_panto_placer_main_1;
    }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_gfx_main,position_in_vehid_chain_from_end){
      0: tk_hb_13000series_end;
      default: sw_tk_hb_13000series_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_lv,vehicle_type_id){
      tk_hb_13000series: tk_hb_13000series_front;
      default: sw_tk_hb_13000series_gfx_main;
    }
//*post fixers
  //capacity-changer
  switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_capacity,position_in_vehid_chain_from_end){
    0: 140;
    default: 151;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_capacity_1,position_in_vehid_chain){
    0: 140;
    default: sw_tk_hb_13000series_capacity;
  }
  switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_capacity_main,vehicle_type_id){
    100..102: sw_tk_hb_13000series_capacity;
    sw_tk_hb_13000series_capacity_1;
  }
  //power fix
  switch(FEAT_TRAINS, SELF, sw_tk_hb_13000series_power_main,position_in_vehid_chain_from_end %9){
    0..5:return 205;
    return 0;
  }
//
item(FEAT_TRAINS, tk_hb_13000series,1032) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_TOKYO_METRO), "13000", string(STR_NAME_SERIES));
        introduction_date: date(2016, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;

        cargo_capacity: 140;
        loading_speed: param_loading_4D;
        cost_factor: 21;
        running_cost_factor: 51;

        speed: 120 km/h;
        track_type: TR_1067_DC;
        power: 410 kW;
        weight: 35 ton;

        climates_available: param_disable_tokyo == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
		additional_text:
      string(STR_TRAIN_DESC,
      string(STR_NARROWGAUGE),
      string(STR_DENKI, string(STR_DC, 1500)),
      string(STR_LOAD_SPEED, string(STR_DOORS, 4)),
      string(STR_FORMATION, string(STR_NUM_CARS_1, 7)),
      string(STR_OPERATOR, string(STR_TOKYOMETRO)),
      string(STR_LINES_USED, string(STR_TOKYO_HIBIYA)));
		purchase: tk_hb_13000series_purchase;
		can_attach_wagon: sw_1067_attach_vehid;
		start_stop: sw_stop_start_3;

		SET_GRAPHICS_DOUBLE(sw_tk_hb_13000series_lv,//graphics
		sw_tk_hb_13000series_capacity_main(),//cargo capacity
		sw_tk_hb_13000series_power_main()*4*1342/1000,//power
		param_loading_4D,//loading speed
		sw_empty_desc,//subtype
		mu_car_narrow,34)//car_fallback,weight
}