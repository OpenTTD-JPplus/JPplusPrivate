//purchase
spriteset(hankyu1000n_purchase_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kansai/hankyu/hankyu1000n/hankyu1000n.png"){ tmpl_purchase_1c(0, 0) }
spriteset(hankyu1000n_purchase_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kansai/hankyu/hankyu1000n/hankyu1000n.png"){ tmpl_purchase_2c(0, 75) }
GRAPHIC_STACK_3(hankyu1000n_purchase, logo_hankyu, hankyu1000n_purchase_c1, hankyu1000n_purchase_c2)
//livery 1
  spriteset(hankyu1000n_1_front, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kansai/hankyu/hankyu1000n/hankyu1000n.png"){ tmpl_high_skirt(0, 0) }
  spriteset(hankyu1000n_1_end, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kansai/hankyu/hankyu1000n/hankyu1000n.png"){ tmpl_high_skirt_rev(0, 25) }
  spriteset(hankyu1000n_1_mot, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kansai/hankyu/hankyu1000n/hankyu1000n.png"){ tmpl_symm(0, 50) }
  spriteset(hankyu1000n_1_wag, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kansai/hankyu/hankyu1000n/hankyu1000n.png"){ tmpl_symm(0, 75) }
//TODO more liveries

//*livery 1
  //panto-placers
    //var 1
    switch(FEAT_TRAINS, SELF, sw_hankyu1000n_1_panto_placer_1,position_in_vehid_chain %6){
      0: hankyu1000n_1_mot;
      4: hankyu1000n_1_mot;
      hankyu1000n_1_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_hankyu1000n_1_panto_placer_1_rev,position_in_vehid_chain_from_end %6 -1){
      0: hankyu1000n_1_mot;
      4: hankyu1000n_1_mot;
      hankyu1000n_1_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_hankyu1000n_1_panto_placer_main_1,vehicle_is_reversed){
      1: sw_hankyu1000n_1_panto_placer_1_rev;
      sw_hankyu1000n_1_panto_placer_1;
    }
    //var 2
    switch(FEAT_TRAINS, SELF, sw_hankyu1000n_1_panto_placer_2,position_in_vehid_chain %3){
      1: hankyu1000n_1_mot;
      hankyu1000n_1_wag;
    }
    switch(FEAT_TRAINS, SELF, sw_hankyu1000n_1_panto_placer_2_rev,position_in_vehid_chain_from_end %3 -1){
      1: hankyu1000n_1_mot;
      hankyu1000n_1_wag;
    }
    switch(FEAT_TRAINS, PARENT, sw_hankyu1000n_1_panto_placer_main_2,vehicle_is_reversed){
      1: sw_hankyu1000n_1_panto_placer_2_rev;
      sw_hankyu1000n_1_panto_placer_2;
    }
    switch(FEAT_TRAINS, SELF, sw_hankyu1000n_1_panto_placer_main,num_vehs_in_vehid_chain+1){
      5: sw_hankyu1000n_1_panto_placer_main_2;
      sw_hankyu1000n_1_panto_placer_main_1;
    }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_hankyu1000n_1_gfx_main,position_in_vehid_chain_from_end){
      0: hankyu1000n_1_end;
      default: sw_hankyu1000n_1_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_hankyu1000n_lv,vehicle_type_id){
      hankyu1000n: hankyu1000n_1_front;
      default: sw_hankyu1000n_1_gfx_main;
    }
//*post fixers
  //capacity-changer
  switch(FEAT_TRAINS, SELF, sw_hankyu1000n_capacity,position_in_vehid_chain_from_end){
    0: 121;
    default: 131;
  }
  switch(FEAT_TRAINS, SELF, sw_hankyu1000n_capacity_1,position_in_vehid_chain){
    0: 121;
    default: sw_hankyu1000n_capacity;
  }
  switch(FEAT_TRAINS, SELF, sw_hankyu1000n_capacity_main,vehicle_type_id){
    mu_car: sw_hankyu1000n_capacity;
    sw_hankyu1000n_capacity_1;
  }
  //power fix
  switch(FEAT_TRAINS, SELF, sw_hankyu1000n_power_main,position_in_vehid_chain_from_end %8){
    1..2:return 190;
    5..6:return 190;
    return 0;
  }
//
item(FEAT_TRAINS, hankyu1000n) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_HANKYU), string(STR_NAME_NEW, "1000/1300"), string(STR_NAME_SERIES));
        introduction_date: date(2013, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cost_factor: 21;
        running_cost_factor: 51;

        speed: 130 km/h;
        track_type: TR_1435_DC;
        weight: 30 ton;
        climates_available: param_disable_hankyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        //bulk
          cargo_capacity: 1;
          power: 1 kW;
          loading_speed: param_loading_4D;
          ai_special_flag: AI_FLAG_PASSENGER;
          sprite_id: SPRITE_ID_NEW_TRAIN;
          misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
          refittable_cargo_classes: bitmask(CC_PASSENGERS);
          non_refittable_cargo_classes: bitmask();                    

          running_cost_base: RUNNING_COST_ELECTRIC;
          engine_class: ENGINE_CLASS_ELECTRIC;
          visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
          
          tractive_effort_coefficient: 0.3;
          air_drag_coefficient: 0.07;
          bitmask_vehicle_info: 0;
          length: 8;
    }
	graphics {
		purchase: hankyu1000n_purchase;
		default: sw_hankyu1000n_lv;
		start_stop: sw_stop_start_3;
		power: sw_hankyu1000n_power_main()*4*1342/1000;
		loading_speed: param_loading_3D;
		cargo_capacity: sw_hankyu1000n_capacity_main()*param_capacity_mod/3;  
		can_attach_wagon: sw_1435_attach_vehid;
		// cargo_subtype_text:sw_hankyu1000n_lv_desc;
		additional_text:
				string(STR_TRAIN_DESC,
				string(STR_STANDARDGAUGE),
				string(STR_DENKI, string(STR_DC_N, 1500)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 3)),
				string(STR_FORMATION, string(STR_NUM_CARS_1, 8)),
				string(STR_USAGE, string(STR_METRO_ACCESS)),
		string( STR_OPERATOR, string(STR_HANKYU)));
    }
	livery_override(mu_car){
		cargo_subtype_text:sw_empty_desc;
        loading_speed: param_loading_3D;
			cargo_capacity: sw_hankyu1000n_capacity_main()*param_capacity_mod/3;
			default: sw_hankyu1000n_lv;
			power: sw_hankyu1000n_power_main()*4*1342/1000;
			weight: 30;
		}
}