//purchase
  spriteset(keisei700_purchase_c1,      "gfx/private/Kanto/keisei/keisei700/keisei700.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(keisei700_purchase_c2,      "gfx/private/Kanto/keisei/keisei700/keisei700.png"){ tmpl_purchase_2c(0, 100) }
  GRAPHIC_STACK_3(keisei700_purchase, logo_keisei, keisei700_purchase_c1, keisei700_purchase_c2)
//livery 1
spriteset (keisei700_1_front_mot,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std(0, 0) }
spriteset (keisei700_1_end_mot,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std_rev(0, 25) }
spriteset (keisei700_1_end_mot_rev,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std(0, 25) }
spriteset (keisei700_1_front,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std(0, 50) }
spriteset (keisei700_1_end,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std_rev(0, 75) }
spriteset (keisei700_1_end_rev,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std(0, 75) }
spriteset (keisei700_1_wag,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_symm(0, 100) }
spriteset (keisei700_1_mot_2,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std(0, 125) }
spriteset (keisei700_1_mot_2_rev,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_std_rev(0, 125) }
spriteset (keisei700_1_wag_2,       "gfx/private/Kanto/keisei/keisei700/keisei700.png") { tmpl_symm(0, 150) }

//*livery 1
  //special gfx
    switch(FEAT_TRAINS, PARENT, sw_keisei700_1_front_gfx,vehicle_is_reversed==0&&num_vehs_in_consist==2){
      1: keisei700_1_front;
      keisei700_1_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_keisei700_1_end_gfx,vehicle_is_reversed==1&&num_vehs_in_consist==2){
      1: keisei700_1_end;
      keisei700_1_end_mot;
    }
  //panto
    //var 1
      switch(FEAT_TRAINS, SELF, sw_keisei700_1_panto_placer_1,position_in_vehid_chain %4){
        2: keisei700_1_end_mot;
        3: keisei700_1_end_mot_rev;
        keisei700_1_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_keisei700_1_panto_placer_1_rev,position_in_vehid_chain_from_end %4){
        3: keisei700_1_end_mot_rev;
        0: keisei700_1_end_mot;
        keisei700_1_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_keisei700_1_panto_placer_main_1,vehicle_is_reversed){
        1: sw_keisei700_1_panto_placer_1_rev;
        sw_keisei700_1_panto_placer_1;
      }
    //var 2
      switch(FEAT_TRAINS, SELF, sw_keisei700_1_panto_placer_2,position_in_vehid_chain %3){
        1: keisei700_1_end_mot;
        2: keisei700_1_end_mot_rev;
        keisei700_1_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_keisei700_1_panto_placer_2_rev,position_in_vehid_chain_from_end %3){
        2: keisei700_1_end_mot_rev;
        0: keisei700_1_end_mot;
        keisei700_1_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_keisei700_1_panto_placer_main_2,vehicle_is_reversed){
        1: sw_keisei700_1_panto_placer_2_rev;
        sw_keisei700_1_panto_placer_2;
      }
    
    switch(FEAT_TRAINS, SELF, sw_keisei700_1_panto_placer_main,num_vehs_in_vehid_chain+1){
      5:sw_keisei700_1_panto_placer_main_2;
      9:sw_keisei700_1_panto_placer_main_2;
      sw_keisei700_1_panto_placer_main_1;
    }
    
	//main
    switch(FEAT_TRAINS, SELF, sw_keisei700_1_gfx_main,position_in_vehid_chain_from_end){
      0: sw_keisei700_1_end_gfx;
      sw_keisei700_1_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_keisei700_livery_1,vehicle_type_id){
      mu_car: sw_keisei700_1_gfx_main;
      sw_keisei700_1_front_gfx;
    }
//
//*livery 2
  //special gfx
    switch(FEAT_TRAINS, PARENT, sw_keisei700_1_front_gfx_2,vehicle_is_reversed){
      1: keisei700_1_front;
      keisei700_1_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_keisei700_1_end_gfx_2,vehicle_is_reversed){
      1: keisei700_1_end_mot;
      keisei700_1_end;
    }
  //panto
    //var 3 (1967)
      switch(FEAT_TRAINS, SELF, sw_keisei700_1_panto_placer_3,position_in_vehid_chain %4){
        1: keisei700_1_mot_2;
        2: keisei700_1_end;
        3: keisei700_1_end_mot_rev;
        keisei700_1_wag_2;
      }
      switch(FEAT_TRAINS, SELF, sw_keisei700_1_panto_placer_3_rev,position_in_vehid_chain_from_end %4){
        2: keisei700_1_mot_2_rev;
        3: keisei700_1_end_rev;
        0: keisei700_1_end_mot;
        keisei700_1_wag_2;
      }
      switch(FEAT_TRAINS, PARENT, sw_keisei700_1_panto_placer_main_3,vehicle_is_reversed){
        1: sw_keisei700_1_panto_placer_3_rev;
        sw_keisei700_1_panto_placer_3;
      }
    
//main
    switch(FEAT_TRAINS, SELF, sw_keisei700_2_gfx_main,position_in_vehid_chain_from_end){
      0: sw_keisei700_1_end_gfx_2;
      sw_keisei700_1_panto_placer_main_3;
    }
    switch(FEAT_TRAINS, SELF, sw_keisei700_livery_2,vehicle_type_id){
      mu_car: sw_keisei700_2_gfx_main;
      sw_keisei700_1_front_gfx_2;
    }

//liveries
  random_switch(FEAT_TRAINS, PARENT, sw_keisei700_livery_0_1){
    1: sw_keisei700_livery_1;
    1: sw_keisei700_livery_2;
  }
  switch(FEAT_TRAINS, PARENT, sw_keisei700_livery_0,build_year){
      0..1967:sw_keisei700_livery_1;
      sw_keisei700_livery_0_1;
    }
  switch(FEAT_TRAINS, SELF, sw_keisei700_lv,cargo_subtype){
    0:sw_keisei700_livery_0;
    1:sw_keisei700_livery_1;
    2:sw_keisei700_livery_2;
  }
  switch(FEAT_TRAINS, SELF, sw_keisei700_lv_desc,cargo_subtype){
    0:string(LV_RANDOM);
    1:string(LV_KEISEI_AODEN);
    2:string(LV_keisei700_2);
  }
//
// post fixers
  //power fix
    switch(FEAT_TRAINS, SELF, sw_keisei700_power_main,position_in_consist_from_end %2){
      0:return 55;
      return 0;
    }
//
item(FEAT_TRAINS, keisei700) {
    property {
        cargo_capacity: 130;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                
        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
        
        climates_available: param_disable_keisei == 0 ? ALL_CLIMATES : NO_CLIMATE;
        name: string(STR_TRAIN_NAME, string(STR_NAME_KEISEI), "700/2200", string(STR_NAME_TYPE));
        introduction_date: date(1954, 1, 1);
        model_life: 30;
        vehicle_life: 20;
        reliability_decay: 10;

        cost_factor: 15;
        running_cost_factor: 45;
        speed: 100 km/h;
        track_type: TR_1435_DC;
        power: 148 kW;
        weight: 37 ton;
        loading_speed: param_loading_3D;
    }
	graphics {
    power: sw_keisei700_power_main()*4*1342/1000;
    default: sw_keisei700_lv;
		purchase: keisei700_purchase;
		can_attach_wagon: sw_1435_attach_vehid;
		cargo_subtype_text:sw_keisei700_lv_desc;
    additional_text:
				string(STR_TRAIN_DESC,
				string(STR_STANDARDGAUGE),
				string(STR_DENKI, string(STR_DC, 1500)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 3)),
				string(STR_FORMATION, string(STR_NUM_CARS_2, 2,4)),
				string(STR_USAGE, string(STR_COMMUTER)),
				string(STR_OPERATOR, string(STR_KEISEI)));

		start_stop: sw_stop_start_2;
        cargo_capacity: 120*param_capacity_mod/3;  
        sound_effect:					sw_KS700_start_sound;
    }
	livery_override(mu_car){
		cargo_subtype_text:sw_keisei700_lv_desc;
      loading_speed: param_loading_3D;
      cargo_capacity: 120*param_capacity_mod/3; 
      default: sw_keisei700_lv;
      power: sw_keisei700_power_main()*4*1342/1000;
      weight: 34;
      }
}