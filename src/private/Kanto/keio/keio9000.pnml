//*purchase
  spriteset(keio9000_front_base_c1, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,	"gfx/private/Kanto/keio/keio9000/keio9000_1.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(keio9000_car_base_c2, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,	"gfx/private/Kanto/keio/keio9000/keio9000_1.png"){ tmpl_purchase_2c(0, 25) }
  GRAPHIC_STACK_7(keio9000_purchase, logo_keio, roof_gray_med_h1_c1, keio9000_front_base_c1, ac_long_toshiba_c1, roof_gray_med_c2, keio9000_car_base_c2, ac_long_toshiba_c2)
//*lights
	spriteset(keio9000_lights_rear, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/keio/keio9000/keio9000_lights.png"){ tmpl_high_skirt(0, 0) }
	spriteset(keio9000_lights_back, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,      "gfx/private/Kanto/keio/keio9000/keio9000_lights.png"){ tmpl_end_v2(0, 25) }
//*livery 1
	spriteset(keio9000_1_front_base, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,	"gfx/private/Kanto/keio/keio9000/keio9000_1.png"){ tmpl_high_skirt(0, 0) }
	spriteset(keio9000_1_end_base, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,	"gfx/private/Kanto/keio/keio9000/keio9000_1.png"){ tmpl_high_skirt_rev(0, 0) }
	spriteset(keio9000_1_car_base, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,	"gfx/private/Kanto/keio/keio9000/keio9000_1.png"){ tmpl_symm(0, 25) }

	TRAIN_CONSTRUCTOR_4(keio9000_1_front, keio9000_1_front_base, roof_gray_med_h1, keio9000_lights_rear, ac_long_toshiba)
	TRAIN_CONSTRUCTOR_4(keio9000_1_end, keio9000_1_end_base, roof_gray_med_h1_rev, keio9000_lights_back, ac_long_toshiba)
	TRAIN_CONSTRUCTOR_3(keio9000_1_wag, keio9000_1_car_base, roof_gray_med, ac_long_toshiba)
	TRAIN_CONSTRUCTOR_4(keio9000_1_mot, keio9000_1_car_base, roof_gray_med, ac_long_toshiba, panto_arm_n_single)
	TRAIN_CONSTRUCTOR_4(keio9000_1_mot_rev, keio9000_1_car_base, roof_gray_med, ac_long_toshiba, panto_arm_n_single_rev)
//
//*livery 1
	//panto placer
		//1 10
			switch(FEAT_TRAINS, SELF, sw_keio9000_1_panto_placer_1,position_in_vehid_chain %6){
				0: keio9000_1_mot;
				1: keio9000_1_mot;
				3: keio9000_1_mot;
				keio9000_1_wag;
			}
			switch(FEAT_TRAINS, SELF, sw_keio9000_1_panto_placer_1_rev,position_in_vehid_chain_from_end %6 -1){
				0: keio9000_1_mot_rev;
				1: keio9000_1_mot_rev;
				3: keio9000_1_mot_rev;
				keio9000_1_wag;
			}
			switch(FEAT_TRAINS, PARENT, sw_keio9000_1_panto_placer_main_1,vehicle_is_reversed){
				1: sw_keio9000_1_panto_placer_1_rev;
				sw_keio9000_1_panto_placer_1;
			}
		//2
			switch(FEAT_TRAINS, SELF, sw_keio9000_1_panto_placer_2,position_in_vehid_chain %4){
				0: keio9000_1_mot;
				1: keio9000_1_mot;
				keio9000_1_wag;
			}
			switch(FEAT_TRAINS, SELF, sw_keio9000_1_panto_placer_2_rev,position_in_vehid_chain_from_end %4 -1){
				0: keio9000_1_mot_rev;
				1: keio9000_1_mot_rev;
				keio9000_1_wag;
			}
			switch(FEAT_TRAINS, PARENT, sw_keio9000_1_panto_placer_main_2,vehicle_is_reversed){
				1: sw_keio9000_1_panto_placer_2_rev;
				sw_keio9000_1_panto_placer_2;
			}
		//panto controller
		switch(FEAT_TRAINS, SELF, sw_keio9000_1_panto_placer_main,num_vehs_in_vehid_chain+1){
			8:sw_keio9000_1_panto_placer_main_2;
			sw_keio9000_1_panto_placer_main_1;
		}
	//front-end-fixer
		switch(FEAT_TRAINS, SELF, sw_keio9000_1_end_gfx_main,position_in_vehid_chain_from_end){
			0: keio9000_1_end;
			sw_keio9000_1_panto_placer_main;
		}
		switch(FEAT_TRAINS, SELF, sw_keio9000_1_front_gfx_main,position_in_vehid_chain_from_end){
			0: keio9000_1_front;
			sw_keio9000_1_panto_placer_main;
		}
		switch(FEAT_TRAINS, SELF, sw_keio9000_lv,vehicle_type_id){
			mu_car:sw_keio9000_1_end_gfx_main;
			sw_keio9000_1_front_gfx_main;
		}
//
//*post fixers
	//capacity-changer
		switch(FEAT_TRAINS, SELF, sw_keio9000_capacity,position_in_vehid_chain_from_end){
			0: 141;
			default: 152;
		}
		switch(FEAT_TRAINS, SELF, sw_keio9000_capacity_1,position_in_vehid_chain){
			0: 141;
			default: sw_keio9000_capacity;
		}
		switch(FEAT_TRAINS, SELF, sw_keio9000_capacity_main,vehicle_type_id){
			mu_car:sw_keio9000_capacity;
			sw_keio9000_capacity_1;
		}
	//power fix
		switch(FEAT_TRAINS, SELF, sw_keio9000_power_main,position_in_vehid_chain_from_end%10){
			1..2:return 170;
			5..6:return 170;
			8:return 170;
			return 0;
		}
//
item(FEAT_TRAINS, keio9000) {
	property {
		//tech/rewritten
			cargo_capacity: 1;
			power: 1 kW;
			ai_special_flag: AI_FLAG_PASSENGER;
			sprite_id: SPRITE_ID_NEW_TRAIN;
			misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
			refittable_cargo_classes: bitmask(CC_PASSENGERS);
			non_refittable_cargo_classes: bitmask();
			running_cost_base: RUNNING_COST_ELECTRIC;
			engine_class: ENGINE_CLASS_ELECTRIC;
			visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
			tractive_effort_coefficient: 0.3;
			air_drag_coefficient: 0.07;
			bitmask_vehicle_info: 0;
		//
		name: string(STR_TRAIN_NAME, string(STR_NAME_KEIO), "9000", string(STR_NAME_SERIES),);
		climates_available: param_disable_keio == 0 ? ALL_CLIMATES : NO_CLIMATE;
		introduction_date: date(2000, 1, 1);
		model_life: VEHICLE_NEVER_EXPIRES;
		vehicle_life: 35;
		reliability_decay: 10;

		cost_factor: 20;
		running_cost_factor: 50;
		speed: 120 km/h;
		track_type: TR_1372_DC;
		weight: 33 ton;
	}
	graphics {
		additional_text:
			string(STR_TRAIN_DESC,
			string(STR_SCOTCHGAUGE),
			string(STR_DENKI, string(STR_DC_N, 1500)),
			string(STR_LOAD_SPEED, string(STR_DOORS, 4),),
			string(STR_FORMATION, string(STR_NUM_CARS_2, 8,10)),
			string(STR_USAGE, string(STR_METRO_ACCESS)),
			string(STR_OPERATOR, string(STR_KEIO)));
		// cargo_subtype_text:sw_keio9000_lv_desc;
		can_attach_wagon: sw_1435_attach_vehid;
		purchase: keio9000_purchase;
		start_stop: sw_stop_start_3;

    SET_GRAPHICS_SINGLE(sw_keio9000_lv,//graphics
    sw_keio9000_capacity_main(),//cargo capacity
    sw_keio9000_power_main()*4*1342/1000,//power
    param_loading_4D,//loading speed
    sw_empty_desc,31)//subtype,weight
}