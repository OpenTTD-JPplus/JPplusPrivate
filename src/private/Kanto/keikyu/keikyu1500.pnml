//purchase
  spriteset(keikyu1500_purchase_c1,      "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(keikyu1500_purchase_c2,      "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png"){ tmpl_purchase_2c(0, 150) }
  GRAPHIC_STACK_3(keikyu1500_purchase, logo_keikyu, keikyu1500_purchase_c1, keikyu1500_purchase_c2)
//livery 1
spriteset (keikyu1500_1_front_mot, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_std(0, 0) }
spriteset (keikyu1500_1_end_mot, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_std_rev(0, 25) }
spriteset (keikyu1500_1_front, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_std(0, 50) }
spriteset (keikyu1500_1_end, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_std_rev(0, 75) }
spriteset (keikyu1500_1_mot, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_std(0, 100) }
spriteset (keikyu1500_1_mot_rev, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_std_rev(0, 100) }
spriteset (keikyu1500_1_mot_2, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_symm(0, 125) }
spriteset (keikyu1500_1_wag, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_1.png") { tmpl_symm(0, 150) }

spriteset (keikyu1500_2_front, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_2.png") { tmpl_std(0, 0) }
spriteset (keikyu1500_2_end, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_2.png") { tmpl_std_rev(0, 25) }
spriteset (keikyu1500_2_front_mot, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_2.png") { tmpl_std(0, 50) }
spriteset (keikyu1500_2_end_mot, "gfx/private/Kanto/keikyu/keikyu1500/keikyu1500_2.png") { tmpl_std_rev(0, 75) }

//*Livery 1
  //special gfx
    //timed
    switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_front_mot,build_year>1989||current_year>2007){
      1: keikyu1500_2_front_mot;
      keikyu1500_1_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_end_mot,build_year>1989||current_year>2007){
      1: keikyu1500_2_end_mot;
      keikyu1500_1_end_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_front,build_year>1989||current_year>2007){
      1: keikyu1500_2_front;
      keikyu1500_1_front;
    }
    switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_end,build_year>1989||current_year>2007){
      1: keikyu1500_2_end;
      keikyu1500_1_end;
    }
    //head fixer
    switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_front_gfx,vehicle_is_reversed){
      1: sw_keikyu1500_1_front;
      sw_keikyu1500_1_front_mot;
    }
    switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_end_gfx,vehicle_is_reversed){
      1: sw_keikyu1500_1_end_mot;
      sw_keikyu1500_1_end;
    }
  //panto-placers
    //var 1 1988
      switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_panto_placer_1,position_in_vehid_chain %2){
        1: keikyu1500_1_mot;
        keikyu1500_1_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_panto_placer_1_rev,position_in_vehid_chain_from_end %2){
        0: keikyu1500_1_mot_rev;
        keikyu1500_1_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_panto_placer_main_1,vehicle_is_reversed){
        1: sw_keikyu1500_1_panto_placer_1_rev;
        sw_keikyu1500_1_panto_placer_1;
      }
    //var 2 1988 8 car
      switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_panto_placer_2,position_in_vehid_chain %7){
        1: keikyu1500_1_mot_2;
        3: keikyu1500_1_mot_2;
        5: keikyu1500_1_mot;
        keikyu1500_1_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_panto_placer_2_rev,position_in_vehid_chain_from_end %7-1){
        1: keikyu1500_1_mot_2;
        3: keikyu1500_1_mot_2;
        5: keikyu1500_1_mot_rev;
        keikyu1500_1_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_panto_placer_main_2,vehicle_is_reversed){
        1: sw_keikyu1500_1_panto_placer_2_rev;
        sw_keikyu1500_1_panto_placer_2;
      }
    //var 3 1989 8 car
      switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_panto_placer_3,position_in_vehid_chain %7){
        3: keikyu1500_1_mot_2;
        5: keikyu1500_1_mot;
        keikyu1500_1_wag;
      }
      switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_panto_placer_3_rev,position_in_vehid_chain_from_end %7-1){
        3: keikyu1500_1_mot_2;
        5: keikyu1500_1_mot_rev;
        keikyu1500_1_wag;
      }
      switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_panto_placer_main_3,vehicle_is_reversed){
        1: sw_keikyu1500_1_panto_placer_3_rev;
        sw_keikyu1500_1_panto_placer_3;
      }
    //8 car main
    switch(FEAT_TRAINS, PARENT, sw_keikyu1500_1_panto_placer_main_0,build_year){
      0..1987: sw_keikyu1500_1_panto_placer_main_1;
      1988:sw_keikyu1500_1_panto_placer_main_2;
      sw_keikyu1500_1_panto_placer_main_3;
      }
    switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_panto_placer_main,num_vehs_in_vehid_chain+1){
      0..6:sw_keikyu1500_1_panto_placer_main_1;
      sw_keikyu1500_1_panto_placer_main_0;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu1500_1_gfx_main,position_in_vehid_chain_from_end){
      0: sw_keikyu1500_1_end_gfx;
      sw_keikyu1500_1_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu1500_lv,vehicle_type_id){
      mu_car: sw_keikyu1500_1_gfx_main;
      sw_keikyu1500_1_front_gfx;
    }
//post fixers
  //capacity-changer
    switch(FEAT_TRAINS, SELF, sw_keikyu1500_capacity,position_in_vehid_chain_from_end){
      0: 123;
      default: 132;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu1500_capacity_1,position_in_vehid_chain){
      0: 123;
      default: sw_keikyu1500_capacity;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu1500_capacity_main,vehicle_type_id){
      mu_car: sw_keikyu1500_capacity;
      sw_keikyu1500_capacity_1;
    }
  
  //power fix
    switch(FEAT_TRAINS, SELF, sw_keikyu1500_power_main,position_in_vehid_chain_from_end%8){
      0..4:return 100;
      return 0;
    }
//

item(FEAT_TRAINS, keikyu1500) {
    property {
        name: string(STR_TRAIN_NAME, string(STR_NAME_KEIKYU), "1500", string(STR_NAME_TYPE));
        introduction_date: date(1985, 1, 1);
        model_life: VEHICLE_NEVER_EXPIRES;
        vehicle_life: 30;
        reliability_decay: 10;

        cargo_capacity: 1;
        loading_speed: param_loading_3D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 130 km/h;
        track_type: TR_1435_DC;
        power: 620 kW;
        weight: 29 ton;

        climates_available: param_disable_keikyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost: 0;
        dual_headed: 0;
        ai_engine_rank: 0;

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        power: sw_keikyu1500_power_main()*4*1342/1000;
        //cargo_subtype_text:sw_keikyu1500_lv_desc;
        default: sw_keikyu1500_lv;
        purchase: keikyu1500_purchase;
        can_attach_wagon: sw_1435_attach_vehid;
        additional_text:
		string(STR_TRAIN_DESC,
		string(STR_STANDARDGAUGE),
		string(STR_DENKI, string(STR_DC_N, 1500)),
		string(STR_LOAD_SPEED, string(STR_DOORS, 3)),
		string(STR_FORMATION, string(STR_NUM_CARS_3, 4,6,8)),
		string(STR_USAGE, string(STR_COMMUTER)),
		string(STR_OPERATOR, string(STR_KEIKYU)));

		start_stop: sw_stop_start_2;
        cargo_capacity: sw_keikyu1500_capacity_main()*param_capacity_mod/3;
        sound_effect: sw_KQ1500_start_sound;
    }
    livery_override(mu_car){
        loading_speed: param_loading_3D;
        cargo_capacity: sw_keikyu1500_capacity_main()*param_capacity_mod/3;
        default:                      sw_keikyu1500_lv;
        power: sw_keikyu1500_power_main()*4*1342/1000;
        weight: 28;
        length: 8;
      }
}