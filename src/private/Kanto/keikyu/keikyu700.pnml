//purchase
  spriteset(keikyu700_purchase_c1,      "gfx/private/Kanto/keikyu/keikyu700/keikyu700_1.png"){ tmpl_purchase_1c(0, 0) }
  spriteset(keikyu700_purchase_c2,      "gfx/private/Kanto/keikyu/keikyu700/keikyu700_1.png"){ tmpl_purchase_2c(0, 50) }
  GRAPHIC_STACK_3(keikyu700_purchase, logo_keikyu, keikyu700_purchase_c1, keikyu700_purchase_c2)
//livery 1
spriteset (keikyu700_1_front,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_1.png") { tmpl_std(0, 0) }
spriteset (keikyu700_1_end,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_1.png") { tmpl_std_rev(0, 25) }
spriteset (keikyu700_1_end_rev,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_1.png") { tmpl_std(0, 25) }
spriteset (keikyu700_1_wag,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_1.png") { tmpl_symm(0, 50) }

spriteset (keikyu700_2_front,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_2.png") { tmpl_std(0, 0) }
spriteset (keikyu700_2_end,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_2.png") { tmpl_std_rev(0, 25) }
spriteset (keikyu700_2_end_rev,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_2.png") { tmpl_std(0, 25) }
spriteset (keikyu700_2_wag,       "gfx/private/Kanto/keikyu/keikyu700/keikyu700_2.png") { tmpl_symm(0, 50) }

//*livery 1
  //special gfx timed
    switch(FEAT_TRAINS, SELF, sw_keikyu700_1_front_gfx, current_year>1980){
      1:keikyu700_2_front;
      keikyu700_1_front;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu700_1_end_gfx, current_year>1980){
      1:keikyu700_2_end;
      keikyu700_1_end;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu700_1_end_rev_gfx, current_year>1980){
      1:keikyu700_2_end_rev;
      keikyu700_1_end_rev;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu700_1_wag_gfx, current_year>1980){
      1:keikyu700_2_wag;
      keikyu700_1_wag;
    }
  //fake head (panto)
    //var 1 4+x
      switch(FEAT_TRAINS, SELF, sw_keikyu700_1_panto_placer_1,position_in_vehid_chain %4){
        2: sw_keikyu700_1_end_gfx;
        3: sw_keikyu700_1_end_rev_gfx;
        default: sw_keikyu700_1_wag_gfx;
      }
      switch(FEAT_TRAINS, SELF, sw_keikyu700_1_panto_placer_1_rev,position_in_vehid_chain_from_end %4){
        3: sw_keikyu700_1_end_rev_gfx;
        0: sw_keikyu700_1_end_gfx;
        default: sw_keikyu700_1_wag_gfx;
      }
      switch(FEAT_TRAINS, PARENT, sw_keikyu700_1_panto_placer_main_1,vehicle_is_reversed){
        1: sw_keikyu700_1_panto_placer_1_rev;
        sw_keikyu700_1_panto_placer_1;
      }
    //var 2 3+x
      switch(FEAT_TRAINS, SELF, sw_keikyu700_1_panto_placer_2,position_in_vehid_chain %3){
        1: sw_keikyu700_1_end_gfx;
        2: sw_keikyu700_1_end_rev_gfx;
        default: sw_keikyu700_1_wag_gfx;
      }
      switch(FEAT_TRAINS, SELF, sw_keikyu700_1_panto_placer_2_rev,position_in_vehid_chain_from_end %3){
        2: sw_keikyu700_1_end_rev_gfx;
        0: sw_keikyu700_1_end_gfx;
        default: sw_keikyu700_1_wag_gfx;
      }
      switch(FEAT_TRAINS, PARENT, sw_keikyu700_1_panto_placer_main_2,vehicle_is_reversed){
        1: sw_keikyu700_1_panto_placer_2_rev;
        sw_keikyu700_1_panto_placer_2;
      }
  
    //panto controller
      switch(FEAT_TRAINS, SELF, sw_keikyu700_1_panto_placer_main,num_vehs_in_vehid_chain+1){
        0..4: sw_keikyu700_1_wag_gfx;
        5..6: sw_keikyu700_1_panto_placer_main_2;
        9: sw_keikyu700_1_panto_placer_main_2;
        sw_keikyu700_1_panto_placer_main_1;
      }
  //front-end-fixer
    switch(FEAT_TRAINS, SELF, sw_keikyu700_1_gfx_main,position_in_vehid_chain_from_end){
        0: sw_keikyu700_1_end_gfx;
        sw_keikyu700_1_panto_placer_main;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu700_lv,vehicle_type_id){
        mu_car: sw_keikyu700_1_gfx_main;
        sw_keikyu700_1_front_gfx;
    }

//post fixers
  //capacity-changer
    switch(FEAT_TRAINS, SELF, sw_keikyu700_capacity,position_in_vehid_chain_from_end){
      0: 135;
      default: 140;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu700_capacity_1,position_in_vehid_chain){
      0: 135;
      default: sw_keikyu700_capacity;
    }
    switch(FEAT_TRAINS, SELF, sw_keikyu700_capacity_main,vehicle_type_id){
      mu_car: sw_keikyu700_capacity;
      sw_keikyu700_capacity_1;
    }
  //power fix
    switch(FEAT_TRAINS, SELF, sw_keikyu700_power_main,position_in_vehid_chain_from_end %4){
      0:return 150;
      3:return 150;
      return 0;
    }
//

item(FEAT_TRAINS, keikyu700) {
    property {
        name: string(STR_TRAIN_NAME_LONG, string(STR_NAME_KEIKYU), "700", string(STR_NAME_TYPE), string(STR_NAME_2_GEN));
        introduction_date: date(1967, 6, 1);
        model_life: 2005-1967-5;
        vehicle_life: 30;
        reliability_decay: 10;

        cargo_capacity: 1;
        loading_speed: param_loading_2D;
        cost_factor: 20;
        running_cost_factor: 50;

        speed: 110 km/h;
        track_type: TR_1435_DC;
        power: 620 kW;
        weight: 29 ton;

        climates_available: param_disable_keikyu == 0 ? ALL_CLIMATES : NO_CLIMATE;
        ai_special_flag: AI_FLAG_PASSENGER;
        sprite_id: SPRITE_ID_NEW_TRAIN;
        misc_flags: bitmask(TRAIN_FLAG_MU, TRAIN_FLAG_SPRITE_STACK);
        refittable_cargo_classes: bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();                

        running_cost_base: RUNNING_COST_ELECTRIC;
        engine_class: ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
        
        tractive_effort_coefficient: 0.3;
        air_drag_coefficient: 0.07;
        bitmask_vehicle_info: 0;
    }
	graphics {
        power: sw_keikyu700_power_main()*4*1342/1000;
		// cargo_subtype_text:sw_keikyu700_lv_desc;
        default: sw_keikyu700_lv;
		purchase: keikyu700_purchase;
		can_attach_wagon: sw_1435_attach_vehid;
		additional_text:
				string(STR_TRAIN_DESC,
				string(STR_STANDARDGAUGE),
				string(STR_DENKI, string(STR_DC_N, 1500)),
				string(STR_LOAD_SPEED, string(STR_DOORS, 4)),
				string(STR_FORMATION, string(STR_NUM_CARS_2, 2,4)),
				string(STR_USAGE, string(STR_COMMUTER)),
				string(STR_OPERATOR, string(STR_KEIKYU)));

		start_stop: sw_stop_start_2;
        cargo_capacity: sw_keikyu700_capacity_main()*param_capacity_mod/3;
        loading_speed: param_loading_4D;
        sound_effect: sw_KQ700_start_sound;
    }
	livery_override(mu_car){
		cargo_subtype_text:sw_empty_desc;
        loading_speed: param_loading_4D;
        cargo_capacity: sw_keikyu700_capacity_main()*param_capacity_mod/3;
        default: sw_keikyu700_lv;
        power: sw_keikyu700_power_main()*4*1342/1000;
        weight: 28;
        length: 8;
      }
}